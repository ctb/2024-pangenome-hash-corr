# run all the scripts, as a basic test!

rule assoc:
    input:
        "agatha-genomes.10k.all.assoc.png",
        "agatha-genomes.10k.all.assoc.pdf",
        expand("agatha-genomes.10k.{p}.assoc.png",
               p=('shell', 'cloud', 'core')),
        expand("agatha-genomes.10k.{p}.assoc.pdf",
               p=('shell', 'cloud', 'core')),
        "agatha-genomes.10k.presence.csv",
        "agatha-genomes.10k.presence.png",
        "agatha-genomes.10k.cluster.tsne.png",

rule calc_genome_presence:
    input:
        ranktable_csv = "ranktable.agathobacter_faecis.csv",
        db = "gtdb-rs214-agatha-k21.zip"
    output: "agatha-genomes.1k.dump"
    shell: """
        ./calc-hash-presence.py {input.ranktable_csv} {input.db} -o {output} --scaled=1000 -k 21
    """

rule make_assoc_matrix_genomes_all:
    input:
        "agatha-genomes.1k.dump",
    output:
        cmp="agatha-genomes.10k.all.assoc",
        labels="agatha-genomes.10k.all.assoc.labels.csv",
        categories="agatha-genomes.10k.all.assoc.categories.csv",
    shell: """
        ./hash-by-hash-assoc.py {input} -o {output.cmp} --scaled=10000 --min=5 \
            -C {output.categories}
    """

rule cluster_assoc_matrix_all:
    input:
        "agatha-genomes.1k.dump",
    output:
        tsne="agatha-genomes.10k.cluster.tsne.png",
        assoc="agatha-genomes.10k.cluster.assoc.png",
        presence="agatha-genomes.10k.cluster.presence.png",
        categories_csv="agatha-genomes.10k.all.assoc.cluster_cats.csv"
    shell: """
        ./cluster-hash-assoc.py {input} --scaled=10000 \
            --output-tsne {output.tsne} \
            --output-assoc {output.assoc} \
            --output-presence {output.presence} \
            --save-categories-csv {output.categories_csv}
    """

rule make_assoc_matrix_genomes_core:
    input:
        "agatha-genomes.1k.dump",
    output:
        cmp="agatha-genomes.10k.core.assoc",
        labels="agatha-genomes.10k.core.assoc.labels.csv",
    shell: """
        ./hash-by-hash-assoc.py {input} -o {output.cmp} --scaled=10000 --min=5 --pangenome 12
    """

rule make_assoc_matrix_genomes_shell:
    input:
        "agatha-genomes.1k.dump",
    output:
        cmp="agatha-genomes.10k.shell.assoc",
        labels="agatha-genomes.10k.shell.assoc.labels.csv",
    shell: """
        ./hash-by-hash-assoc.py {input} -o {output.cmp} --scaled=10000 --min=5 --pangenome 3
    """

rule make_assoc_matrix_genomes_cloud:
    input:
        "agatha-genomes.1k.dump",
    output:
        cmp="agatha-genomes.10k.cloud.assoc",
        csv="agatha-genomes.10k.cloud.assoc.labels.csv",
        cat_csv="agatha-genomes.10k.cloud.assoc.categories.csv",
    shell: """
        ./hash-by-hash-assoc.py {input} -o {output.cmp} --scaled=10000 --min=5 --pangenome 12345 --categories {output.cat_csv}
    """

rule make_hash_by_sample:
    input:
        dump="agatha-genomes.1k.dump",
    output:
        presence="agatha-genomes.10k.presence.csv",
        cat="agatha-genomes.10k.presence.categories.csv",
    shell: """
        ./hash-by-sample.py {input} -o {output.presence} \
            --categories-csv {output.cat} --min=5 --scaled=10000
    """

rule make_clustermap:
    input:
        presence="agatha-genomes.10k.presence.csv",
        cat="agatha-genomes.10k.presence.categories.csv",
    output:
        "agatha-genomes.10k.presence.png"
    shell: """
        sourmash scripts clustermap1 {input.presence} -u presence \
            -C {input.cat} -o {output} --boolean --no-labels
    """

rule make_png_plot:
    input:
        cmp="{cmp}",
        cmp_csv="{cmp}.labels.csv",
    output: "{cmp}.png"
    shell: "sourmash scripts plot2 {input.cmp} {input.cmp_csv} -o {output}"

rule make_pdf_plot:
    input:
        cmp="{cmp}",
        cmp_csv="{cmp}.labels.csv",
    output: "{cmp}.pdf"
    shell: "sourmash scripts plot2 {input.cmp} {input.cmp_csv} -o {output}"
